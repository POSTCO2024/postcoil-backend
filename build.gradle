buildscript {
    ext {
        restdocsApiSpecVersion = '0.16.4'
        swaggerUiVersion = '4.15.5'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}" apply false
    id 'org.hidetake.swagger.generator' version '2.18.2' apply false
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

allprojects {
    group = 'com.postco'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/libs-release" }
        maven { url "https://repo.spring.io/milestone" }
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.epages.restdocs-api-spec'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        // spring
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // logging
        implementation 'org.slf4j:slf4j-api:1.7.36'

        // model mapper
        implementation group: 'org.modelmapper', name: 'modelmapper', version: '3.1.1'

        // restdocs-api-spec
        testImplementation "com.epages:restdocs-api-spec-mockmvc:${restdocsApiSpecVersion}"

        // Swagger UI
        implementation "org.springdoc:springdoc-openapi-ui:1.6.14"

        // dev
        compileOnly 'org.springframework.boot:spring-boot-devtools'

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
        testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'
        testImplementation 'io.rest-assured:rest-assured'
    }

    test {
        useJUnitPlatform()
    }
}
